#!/usr/bin/env zsh
# shellcheck shell=bash
OS=$(uname | tr '[:upper:]' '[:lower:]')
ARCH=$(uname -m)
if [[ "$ARCH" == "x86_64" ]]; then
  ARCH="amd64"
fi
isWSL=$([[ -f /proc/sys/fs/binfmt_misc/WSLInterop ]] && echo "true" || echo "false")

# start xorg
if [[ -t 0 && $(tty) == "/dev/tty1" && ! $DISPLAY ]]; then
  exec startx
fi

# Ensure we have access to brew on macOS
brewPrefix=""
if [[ "$OS" == "darwin" ]] && ! command -v "brew" >/dev/null; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
  brewPrefix=$(brew --prefix)
fi

# 1Password ssh-agent
export SSH_AUTH_SOCK="$HOME/.1password/agent.sock"

## WSL2 Support
# Check if we're running in WSL2
if [[ "$isWSL" == "true" ]]; then
  # Windows specific paths to prevent issues with syntax highlighting speed.
  export PATH="$PATH:/mnt/c/Program Files/Microsoft VS Code/bin"
  export PATH="$PATH:/mnt/c/Program Files/Docker/Docker/resources/bin"
  export PATH="$PATH:/mnt/c/WINDOWS"

  if ! command -v npiperelay.exe >/dev/null; then
    echo "Please install npiperelay.exe from https://github.com/albertony/npiperelay"
    sleep 15
    exit 0
  fi

  # Configure ssh forwarding as per: https://1password.community/discussion/128023/ssh-agent-on-windows-subsystem-for-linux
  # need `ps -ww` to get non-truncated command for matching
  # use square brackets to generate a regex match for the process we want but that doesn't match the grep command running it!
  if ! ps -auxww | grep -q "[n]piperelay.exe -ei -s //./pipe/openssh-ssh-agent"; then
    # Ensure the directory exists
    if [[ ! -e "$HOME/.1password" ]]; then
      mkdir -p "$HOME/.1password"
    fi

    # If the SSH_AUTH_SOCK already exists, remove it because no forwarding command is running.
    if [[ -S $SSH_AUTH_SOCK ]]; then
      rm -f "$SSH_AUTH_SOCK"
    fi

    # setsid to force new session to keep running
    # set socat to listen on $SSH_AUTH_SOCK and forward to npiperelay which then forwards to openssh-ssh-agent on windows
    (setsid socat UNIX-LISTEN:"$SSH_AUTH_SOCK",fork EXEC:"npiperelay.exe -ei -s //./pipe/openssh-ssh-agent",nofork &) >/dev/null 2>&1
  fi
fi

# start tmux
export ZSH_TMUX_AUTOSTART=true

# fzf configuration
export FZF_DEFAULT_COMMAND=fd

# Setup zgenom
ZGENOM_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zgenom"
[[ ! -d $ZGENOM_HOME ]] && mkdir -p "$(dirname $ZGENOM_HOME)"
[[ ! -d $ZGENOM_HOME/.git ]] && git clone https://github.com/jandamm/zgenom.git "$ZGENOM_HOME"
source "$ZGENOM_HOME/zgenom.zsh"

if ! zgenom saved; then
  # Load all libraries from ohmyzsh
  zgenom ohmyzsh

  # Plugins to load from ohmyzsh
  omzsh_plugins=("tmux" "kubectl" "fzf" "git" "docker")
  for omzsh_plugin in "${omzsh_plugins[@]}"; do
    zgenom ohmyzsh "plugins/$omzsh_plugin"
  done

  # Plugins
  if [[ "$isWSL" == "false" ]]; then
    # Only load syntax highlighting when not on WSL, something is broken right now.
    zgenom load "zdharma-continuum/fast-syntx-highlighting"
  fi
  zgenom load "zsh-users/zsh-autosuggestions"
  zgenom load qoomon/zsh-lazyload

  # Theme
  zgenom load "jackharrisonsherlock/common" common.zsh-theme

  # save all to init script
  zgenom save

  # Compile your zsh files
  zgenom compile "$HOME/.zshrc"
  zgenom compile "$HOME/.zsh_profile"
fi

# completion & other not important things
lazyload rtx -- 'source <(rtx activate zsh)'
lazyload kubectl -- 'source <(kubectl completion zsh)'

### BEGIN MY MODIFICATIONS

# custom prompt
PROMPT="%{$fg[cyan]%}日本 $PROMPT"

# general aliases
alias kg='k get'
alias kd='k describe'
alias kdel='k delete'

# cat -> bat, syntax highlighting
if command -v bat >/dev/null 2>&1 || command -v batcat >/dev/null 2>&1; then
  commandName="bat"
  if command -v batcat >/dev/null 2>&1; then
    commandName="batcat"

    # nice to have, alias bat to batcat
    alias bat="$commandName"
  fi
  alias cat="$commandName --tabs 2"
fi

# Replace ls with a better version ;)
if command -v lsd >/dev/null 2>&1; then
  alias ls='lsd --group-dirs first'
fi

# Go
export GOPATH="$HOME/go"
