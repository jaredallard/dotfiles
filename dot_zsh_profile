#!/usr/bin/env zsh
# vim: set ai ts=2 sw=2 et sts=2 ft=zsh :
# shellcheck shell=bash
ARCH=$(uname -m)
if [[ "$ARCH" == "x86_64" ]]; then
  ARCH="amd64"
fi

# DOTFILES_HOME is the location of dotfile scripts and other
# configuration applicable to my dotfiles.
DOTFILES_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/dotfiles"
EXTENSIONS_DIR="$DOTFILES_HOME/extensions"

# CONFIG_FILE is the location of the configuration file for the dotfiles.
CONFIG_FILE="$DOTFILES_HOME/config"
if [[ ! -e "$CONFIG_FILE" ]]; then
  # Copy the default config file
  cp "$DOTFILES_HOME/config.example" "$CONFIG_FILE"
fi

# Load the configuration file
if [[ -e "$CONFIG_FILE" ]]; then
  source "$CONFIG_FILE" || {
    echo "Failed to load config file at '$CONFIG_FILE'" 2>/dev/null
    echo "Refusing to load the rest of the dotfiles" 2>/dev/null
    return
  }
fi

# load_hooks loads all hooks from a directory with the provided
# name. Only loads files ending in .plugin.zsh.
#
# $1: The name of the hook to load
# $2: The directory to load hooks from (optional) (default: $DOTFILES_HOME/zshrc.d)
load_hooks() {
  local hookName="$1"
  local hooksDirectory="${2:-$DOTFILES_HOME/zshrc.d}"

  [[ -d "$hooksDirectory/$hookName" ]] && for file in "$hooksDirectory/$hookName"/*.plugin.zsh; do
    source "$file"
  done
}

# Load custom hooks from dotfiles
for repoURL in "${EXTRA_EXTENSION_REPOS[@]}"; do
  repoName=$(basename "$repoURL")
  if [[ ! -d "$EXTENSIONS_DIR/$repoName" ]]; then
    echo " --> Fetching dotfiles extension from $repoName"
    git clone "$repoURL" "$EXTENSIONS_DIR/$repoName"
  fi
done

# Load .zshrc.d/pre-zgenom-load from extensions
for repo in "$EXTENSIONS_DIR/"*(N); do
  if [[ ! -d "$repo" ]]; then
    continue
  fi

  load_hooks "pre-zgenom-load" "$repo/zshrc.d"
done

# Load all files in $DOTFILES_HOME/zshrc.d/pre-zgenom-load
load_hooks "pre-zgenom-load"

# Setup zgenom
ZGENOM_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zgenom"
[[ ! -d $ZGENOM_HOME ]] && mkdir -p "$(dirname "$ZGENOM_HOME")"
[[ ! -d $ZGENOM_HOME/.git ]] && git clone https://github.com/jandamm/zgenom.git "$ZGENOM_HOME"
source "$ZGENOM_HOME/zgenom.zsh"

# Automatically update zgenom every 7 days
zgenom autoupdate

# Load libraries only if we haven't already generated an init.zsh
if ! zgenom saved; then
  # Load all libraries from ohmyzsh
  zgenom ohmyzsh

  # Plugins to load from ohmyzsh
  omzsh_plugins=("tmux" "kubectl" "fzf" "git" "docker")
  for omzsh_plugin in "${omzsh_plugins[@]}"; do
    zgenom ohmyzsh "plugins/$omzsh_plugin"
  done

  # Plugins
  zgenom load "zdharma-continuum/fast-syntax-highlighting"
  zgenom load "zsh-users/zsh-autosuggestions"
  zgenom load "qoomon/zsh-lazyload"

  # Theme
  zgenom load "jaredallard/alien-minimal"

  # Load plugins from $EXTRA_ZGENOM_PLUGINS
  for plugin in "${EXTRA_ZGENOM_PLUGINS[@]}"; do
    zgenom load "$plugin"
  done

  # save all to init script
  zgenom save

  # Compile your zsh files to speed up startup
  zgenom compile "$HOME/.zshrc"
  zgenom compile "$HOME/.zsh_profile"
  [[ -d "$DOTFILES_HOME/zshrc.d/" ]] && for dir in "$DOTFILES_HOME/zshrc.d/"*; do
    if [[ ! -d "$dir" ]]; then
      continue
    fi

    for file in "$dir"/*.plugin.zsh; do
      zgenom compile "$file"
    done
  done

  # Compile all files in $EXTENSIONS_DIR/*/zshrc.d/*/*.plugin.zsh
  for repo in "$EXTENSIONS_DIR/"*(N); do
    if [[ ! -d "$repo" ]]; then
      continue
    fi

    # Compile all files in $repo/zshrc.d/*/*.plugin.zsh
    for dir in "$repo/zshrc.d/"*; do
      if [[ ! -d "$dir" ]]; then
        continue
      fi

      for file in "$dir"/*.plugin.zsh; do
        zgenom compile "$file"
      done
    done
  done
fi

# Load .zshrc.d/post-zgenom-load from extensions
for repo in "$EXTENSIONS_DIR/"*(N); do
  if [[ ! -d "$repo" ]]; then
    continue
  fi

  load_hooks "post-zgenom-load" "$repo/zshrc.d"
done

# Load all files in $DOTFILES_HOME/zshrc.d/post-zgenom-load
load_hooks "post-zgenom-load"

# Remove duplicates from PATH
typeset -U PATH
