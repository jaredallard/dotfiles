#!/usr/bin/env zsh
# vim: set ai ts=2 sw=2 et sts=2 ft=zsh :
# shellcheck shell=bash
OS=$(uname | tr '[:upper:]' '[:lower:]')
ARCH=$(uname -m)
if [[ "$ARCH" == "x86_64" ]]; then
  ARCH="amd64"
fi

# DOTFILES_HOME is the location of dotfile scripts and other
# configuration applicable to my dotfiles.
DOTFILES_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/dotfiles"

# Load all files in $DOTFILES_HOME/zshrc.d/post-zgenom-load
[[ -d "$DOTFILES_HOME/zshrc.d/pre-zgenom-load" ]] && for file in "$DOTFILES_HOME/zshrc.d/pre-zgenom-load"/*.plugin.zsh; do
  source "$file"
done

# Setup zgenom
ZGENOM_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zgenom"
[[ ! -d $ZGENOM_HOME ]] && mkdir -p "$(dirname "$ZGENOM_HOME")"
[[ ! -d $ZGENOM_HOME/.git ]] && git clone https://github.com/jandamm/zgenom.git "$ZGENOM_HOME"
source "$ZGENOM_HOME/zgenom.zsh"

# Automatically update zgenom every 7 days
zgenom autoupdate

# Load libraries only if we haven't already generated an init.zsh
if ! zgenom saved; then
  # Load all libraries from ohmyzsh
  zgenom ohmyzsh

  # Plugins to load from ohmyzsh
  omzsh_plugins=("tmux" "kubectl" "fzf" "git" "docker" "golang")
  for omzsh_plugin in "${omzsh_plugins[@]}"; do
    zgenom ohmyzsh "plugins/$omzsh_plugin"
  done

  # Plugins
  zgenom load "zdharma-continuum/fast-syntax-highlighting"
  zgenom load "zsh-users/zsh-autosuggestions"
  zgenom load "qoomon/zsh-lazyload"

  # Theme
  zgenom load "jackharrisonsherlock/common" common.zsh-theme

  # save all to init script
  zgenom save

  # Compile your zsh files
  zgenom compile "$HOME/.zshrc"
  zgenom compile "$HOME/.zsh_profile"
  [[ -d "$DOTFILES_HOME/zshrc.d/" ]] && for dir in "$DOTFILES_HOME/zshrc.d/"*; do
    if [[ ! -d "$dir" ]]; then
      continue
    fi

    for file in "$dir"/*.plugin.zsh; do
      zgenom compile "$file"
    done
  done
fi

# Load all files in $DOTFILES_HOME/zshrc.d/post-zgenom-load
[[ -d "$DOTFILES_HOME/zshrc.d/post-zgenom-load" ]] && for file in "$DOTFILES_HOME/zshrc.d/post-zgenom-load"/*.plugin.zsh; do
  source "$file"
done

# Remove duplicates from PATH
typeset -U PATH
